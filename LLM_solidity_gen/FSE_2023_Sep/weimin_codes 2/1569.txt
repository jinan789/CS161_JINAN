contract SwapProxy is Multicall {

    using SafeERC20 for IERC20;
    receive() external payable {}

    address public pancakeV3;
    address public pancakeV2;
    address public uniswap;

    constructor(address _pancakeV2, address _pancakeV3, address _uniswap) {
        pancakeV2 = _pancakeV2;
        pancakeV3 = _pancakeV3;
        uniswap = _uniswap;
    }

    function safeTransferETH(address to, uint256 value) internal {
        (bool success, ) = to.call{value: value}(new bytes(0));
        require(success, 'STE');
    }

    function depositWETH(address weth) external payable {
        IWETH(weth).deposit{value: msg.value}();
    }

    function unwrapWETH(address weth, address recipient) external {
        uint256 all = IWETH(weth).balanceOf(address(this));
        IWETH(weth).withdraw(all);
        safeTransferETH(recipient, all);
    }

    function refundETH() external payable {
        if (address(this).balance > 0) safeTransferETH(msg.sender, address(this).balance);
    }

    function depositWrapToken(address wrapToken, uint256 value) external {
        IWrapToken(wrapToken).depositFrom(msg.sender, address(this), value);
    }

    function withdrawWrapToken(address wrapToken, address recipient) external {
        uint256 value = IWrapToken(wrapToken).balanceOf(address(this));
        if (value > 0) {
            IWrapToken(wrapToken).withdraw(recipient, value);
        }
    }

    function depositToken(address token, uint256 value) external {
        IERC20(token).safeTransferFrom(msg.sender, address(this), value);
    }
    
    function sweepToken(
        address token,
        address recipient
    ) external {
        uint256 all = IERC20(token).balanceOf(address(this));
        if (all > 0) {
            IERC20(token).safeTransfer(recipient, all);
        }
    }

    function approveToken(address token, address spender) external {
        bool ok = IERC20(token).approve(spender, type(uint256).max);
        require(ok, 'approve fail');
    }

    function proxy(address targetContract, bytes calldata data, uint256 msgValue) external payable returns (bytes memory res){
        require(targetContract != address(0), "TARGET IS ZERO!");
        require(targetContract == pancakeV2 || targetContract == pancakeV3 || targetContract == uniswap, "TARGET ERROR");
        require(address(this).balance >= msgValue, "ETH NOT ENOUGH!");
        (bool success, bytes memory result) = targetContract.call{value: msgValue}(data);
        
        if (!success) {
            // Next 5 lines from https://ethereum.stackexchange.com/a/83577
            if (result.length < 68) revert();
            assembly {
                result := add(result, 0x04)
            }
            revert(abi.decode(result, (string)));
        }
        return result;
    }

}
